<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.26">
  <POU Name="FB_DynamicFifo" Id="{4bb7b716-457b-4573-86dd-15ad020b094a}">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_DynamicFifo
VAR
	/// Configuration
	m_bFreeDataPointer: BOOL;
	/// Actual Count of the elements in the buffer
	m_uinItemCount: UINT;
	/// Index of the first added element
	m_pFirstElement: POINTER TO T_QueueBase;
	/// Index of the last added element
	m_pLastElement: POINTER TO T_QueueBase;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="Peek" Id="{3b1f229e-a874-4c38-b5f5-351e19a6bdbe}">
      <Declaration><![CDATA[{attribute 'object_name' := 'Peek'}
METHOD Peek : POINTER TO BYTE
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Peek := TC3_MEMMAN.NULL;

IF m_uinItemCount > 0 THEN
	Peek := m_pFirstElement^.pData;
END_IF]]></ST>
      </Implementation>
      <ObjectProperties />
    </Method>
    <Method Name="Pop" Id="{41280579-af45-4a83-b176-253aba8c8a30}">
      <Declaration><![CDATA[{attribute 'object_name' := 'Pop'}
METHOD Pop : POINTER TO BYTE
VAR
	pItem: POINTER TO T_QueueBase;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Pop := TC3_MEMMAN.NULL;

IF m_uinItemCount > 0 THEN
	pItem := m_pFirstElement;
	
	m_pFirstElement := m_pFirstElement^.pNext;

	IF m_pFirstElement <> TC3_MEMMAN.NULL THEN
		m_pFirstElement^.pPrevious := TC3_MEMMAN.NULL;
	ELSE
		m_pLastElement := TC3_MEMMAN.NULL;
	END_IF

	m_uinItemCount := m_uinItemCount - 1;

	Pop := pItem^.pData;
	__DELETE(pItem);
END_IF

]]></ST>
      </Implementation>
      <ObjectProperties />
    </Method>
    <Method Name="ClearFifo" Id="{63e29c87-2282-4dbe-9377-945d47342c5d}">
      <Declaration><![CDATA[{attribute 'object_name' := 'ClearFifo'}
METHOD ClearFifo : BOOL
VAR
	pItem: POINTER TO T_QueueBase;
	pNextItem: POINTER TO T_QueueBase;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[pItem := m_pFirstElement;
m_pFirstElement := NULL;

WHILE pItem <> TC3_MEMMAN.NULL DO
	pNextItem := pItem^.pNext;
	
	IF m_bFreeDataPointer THEN
		__DELETE(pItem^.pData);
	END_IF
	
	__DELETE(pItem);
	pItem := pNextItem;
END_WHILE

m_pLastElement := NULL;
m_uinItemCount := 0;
]]></ST>
      </Implementation>
      <ObjectProperties />
    </Method>
    <Property Name="FreeDataPointer" Id="{bf969ff9-a951-4563-8abe-f8d84b2bb1f9}">
      <Declaration><![CDATA[PROPERTY FreeDataPointer : BOOL
]]></Declaration>
      <Set Name="Set" Id="{84bcd788-df9b-4f55-b37b-ac7e0dd4bd41}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[m_bFreeDataPointer := FreeDataPointer;]]></ST>
        </Implementation>
        <ObjectProperties />
      </Set>
      <Get Name="Get" Id="{c3a88562-dbe9-456c-86ac-f9c2483836e6}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[FreeDataPointer := m_bFreeDataPointer;]]></ST>
        </Implementation>
        <ObjectProperties />
      </Get>
      <ObjectProperties />
    </Property>
    <Method Name="Push" Id="{d8033914-a83b-4859-b9b2-1586ea6c1832}">
      <Declaration><![CDATA[{attribute 'object_name' := 'Push'}
METHOD Push : INT
VAR_INPUT
	pData: POINTER TO BYTE;
END_VAR
VAR
	pItem: POINTER TO T_QueueBase;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[pItem := __NEW(T_QueueBase);
pItem^.pData := pData;

IF m_uinItemCount = 0 THEN
	m_pFirstElement := pItem;
	m_pLastElement  := pItem;
	pItem^.pNext := TC3_MEMMAN.NULL;
	pItem^.pPrevious := TC3_MEMMAN.NULL;
ELSE
	m_pLastElement^.pNext:= pItem;
	pItem^.pPrevious := m_pLastElement;
	pItem^.pNext := TC3_MEMMAN.NULL;
	m_pLastElement  := pItem;
END_IF

m_uinItemCount := m_uinItemCount + 1;

Push := UINT_TO_INT(m_uinItemCount);
]]></ST>
      </Implementation>
      <ObjectProperties />
    </Method>
    <Property Name="ItemCount" Id="{e0bd556e-f30d-4642-af79-59ae65cd6ba5}">
      <Declaration><![CDATA[PROPERTY ItemCount : UINT
]]></Declaration>
      <Get Name="Get" Id="{60f55f4f-56f0-488e-9a39-772db2c99ef0}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ItemCount := m_uinItemCount;]]></ST>
        </Implementation>
        <ObjectProperties />
      </Get>
      <ObjectProperties />
    </Property>
    <ObjectProperties />
  </POU>
</TcPlcObject>