<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.26">
  <POU Name="FB_BCD" Id="{9b52d7b0-8644-4655-8f02-ec7e053d1595}">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_BCD
VAR
	bolInitDone: BOOL;
	bolActivate: BOOL := TRUE;
	/// driver specific interface
	SIf: T_BCD;
	stStateMachine: T_BCD_StateMachine;
	stSubStateMachine: T_BCD_StateMachine;
	/// Create/release TCP/IP connection
	fbClientConnection: FB_ClientServerConnection;
	/// Send TCP/IP data
	fbSend: FB_SocketSend;
	/// Receive TCP/IP data
	fbReceive: FB_SocketReceive;
	/// Rx data buffer
	aRxData: ARRAY[0..300] OF BYTE;
	/// Tx data
	rTxData				: T_BCD_TxStructure;

	intTxState: INT;
	bolExecuteSend: BOOL;
	/// counter for sended frames 
	udiNSend: UDINT;
	intRxState: INT;
	bolExecuteRead: BOOL;
	/// Count of received data bytes
	udiReceivedBytes: UDINT;
	strReceivedMessage: STRING(255);
	iStringLength: INT;
	/// bar code reader type
	iReaderType: E_BCD_Type;
END_VAR
VAR CONSTANT
	cBCD_SUB_Send: INT := 2000;
	cBCD_SUB_Wait: INT := 2001;
	cBCD_SUB_Read: INT := 2002;
	cBCD_SUB_CheckAnswer: INT := 2003;
	cBCD_SUB_GetData: INT := 2004;
	cBCD_SUB_Done: INT := 2005;
	cBCD_SUB_Error: INT := 2006;
	cTxIdle: USINT := 0;
	cTxBusy: USINT := 1;
	cRxIdle: USINT := 0;
	cRxBusy: USINT := 1;
	cRxConv: USINT := 2;
	cSTX: USINT := 16#2;
	cETX: USINT := 16#3;
	cCR: USINT := 16#D;
	cLF: USINT := 16#A;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*__________________________________________________________________________________________________
¦	
¦	Vistaprint Schweiz GmbH
¦	CH - 8401 Winterthur
¦
¦	www.vistaprint.ch - info@vistaprint.ch
¦___________________________________________________________________________________________________

Function desription:
TCP-IP driver for SICK barcode reader CLV632-0120
Note: This driver work as a TCP client.

History:
Version		Date			Author		Comment
---------------------------------------------------------------------------------------------------
0.00.1		18.12.2013		AVME/DSC	Start history
0.00.2		03.01.2014		AVME/DSC	Basic functionalities implemented and tested. Following commands are supported:
										CMD_CONNECT_SOCKET 	1002 		// Connect TCP socket
										CMD_CLOSE_SOCKET 	1004 		// Close TCP socket
										CMD_COM_TEST 		1006 		// Communication test 
										CMD_READ_BARCODE 	1014 		// Command enable the sensor gate for X-TIME ("duration" setting of sensor object trigger)
																		// Sensor try to detect a barcode.
0.00.3		27.01.2014		AVME/DSC    Added new method "CheckSensorError" to set "bolSensorError" in case if sensor answer with SOPAS error																					 							
0.00.4		24.10.2014		HLV/MAH		driver can be used for Sick or Keyence scanner configured by SIf.Cfg.iSensorType := E_BCD_Type.xxxx
										the port of Keyence scanner must be always 9004
										if Keyence scanner is used, FB_SocketCloseAll must be called in main PRG (see MiddlePRG in BaseElementsSample)

	
___________________________________________________________________________________________________*)


IF (bolActivate) THEN // cyclic call enabled
	
	IF (bolInitDone=FALSE) THEN // first cycle -> init
		CtrlInit();	
		bolInitDone := TRUE;
	ELSE
		CycleStartDuty();
				
		StateMachine();
		
		CycleEndDuty();
	END_IF
	
END_IF;]]></ST>
    </Implementation>
    <Method Name="sta_ConnectSocketBusy" Id="{1ad3cc92-44b3-41f8-8ba2-1e11181ec2a9}">
      <Declaration><![CDATA[{attribute 'object_name' := 'sta_ConnectSocketBusy'}
METHOD PROTECTED sta_ConnectSocketBusy : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (stStateMachine.intState<>stStateMachine.intStateLastCycle) THEN
		stStateMachine.intStateHistory := stStateMachine.intStateLastCycle;
		stStateMachine.intStateLastCycle := stStateMachine.intState;
			
		// Initialize and connect socket
		fbClientConnection.nMode := CONNECT_MODE_ENABLEDBG; 			// not used
		fbClientConnection.sRemoteHost := SIf.stCfg.strSensorIPAdr;
		fbClientConnection.nRemotePort := SIf.stCfg.udiSensorPort;
		fbClientConnection.tReconnect := SIf.stPar.tTOConnection;
		fbClientConnection.bEnable:= TRUE;

	END_IF

//--------------------------------------------------------------------------------------
// Cyclic action
	IF (stStateMachine.intState=stStateMachine.intStateLastCycle) THEN
		
	
		// State Change Condition

		// Check about FUB error	
		IF (fbClientConnection.bError = TRUE) OR (fbClientConnection.nErrId <> 0) THEN
			SIf.stErr.bolErrorActive := TRUE;	
			SIf.stErr.bolSocketError := TRUE;
			SIf.stErr.udiErrorID := fbClientConnection.nErrId;
			stStateMachine.intStateNext := cBCD_STA_Error;
		END_IF
	
		// Wait for socket handle ID	
		IF (fbClientConnection.hSocket.handle <> 0) AND (fbClientConnection.eState = eSOCKET_CONNECTED) THEN
			stStateMachine.intStateNext := cBCD_STA_WaitForJob;
		END_IF

	END_IF

//--------------------------------------------------------------------------------------
// Exit action
	IF (stStateMachine.intState<>stStateMachine.intStateNext) THEN
		SIf.intCmd := cBCD_CMD_DONE;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="CycleStartDuty" Id="{322a3606-8adb-415b-a8c0-2d06fa66396c}">
      <Declaration><![CDATA[{attribute 'object_name' := 'CycleStartDuty'}
METHOD PROTECTED CycleStartDuty : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// ----------------------------------------------------------------------------------
// cyclic actions outside of statemachine

// Simulation active
IF (SIf.stCfg.bolSimulation) THEN
	;
ELSE
	;
END_IF

// Cyclic call of the client connection
fbClientConnection();
// Cyclic call of method "HandleRxData"
THIS^.HandleRxData();

// Generate socket connected status info
IF (fbClientConnection.eState = eSOCKET_CONNECTED) THEN
	SIf.stStatus.bolSocketConnected:= TRUE;
ELSE
	SIf.stStatus.bolSocketConnected:= FALSE;	
	SIf.stStatus.bolComChecked:= FALSE;
END_IF

// Command close socket can be executed all the time.
// Used for error recovery in case if parent controller has an error.
IF (SIf.intCmd=cBCD_CMD_CLOSE_SOCKET) THEN	
	stStateMachine.intStateNext := cBCD_STA_CloseSocketBusy;
END_IF
			
SIf.strCmd := THIS^.getCmdState(intCmdState:=SIf.intCmd,bolState:=FALSE);]]></ST>
      </Implementation>
    </Method>
    <Method Name="PrepareReadMsg" Id="{3d4dade1-2577-4970-9e9b-2f8226f80caf}">
      <Declaration><![CDATA[{attribute 'object_name' := 'PrepareReadMsg'}
METHOD PRIVATE PrepareReadMsg
VAR
	uinI: UINT;
	strCharacter: STRING(1);
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Delete last message
strReceivedMessage:= '';

// Loop over receive buffer
FOR uinI:= 0 TO SIZEOF(aRxData)-1 DO	
	// Start of text?
	IF (aRxData[uinI] = cSTX) THEN
		;
	// End of text?
	ELSIF (aRxData[uinI] = cETX) THEN
		EXIT;
	ELSE
		// Convert ASCII to string
		strCharacter:= F_ToCHR(aRxData[uinI]);
		strReceivedMessage:= CONCAT(strReceivedMessage,strCharacter);			
	END_IF
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_SocketClosed" Id="{434f1ca1-1c54-45bf-96b7-273daaa6f39e}">
      <Declaration><![CDATA[{attribute 'object_name' := 'sta_SocketClosed'}
METHOD PROTECTED sta_SocketClosed : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (stStateMachine.intState<>stStateMachine.intStateLastCycle) THEN
		stStateMachine.intStateLastCycle := stStateMachine.intState;
		;
	END_IF

//--------------------------------------------------------------------------------------
// Cyclic action
	IF (stStateMachine.intState=stStateMachine.intStateLastCycle) THEN

		// State Change Condition
		IF (SIf.intCmd=cBCD_CMD_CONNECT_SOCKET) THEN
			stStateMachine.intStateNext := cBCD_STA_ConnectSocketBusy;
		END_IF
		
	END_IF

//--------------------------------------------------------------------------------------
// Exit action
	IF (stStateMachine.intState<>stStateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_CloseSocketBusy" Id="{5d499f6a-81ba-4ae4-9b9a-1876c9564350}">
      <Declaration><![CDATA[{attribute 'object_name' := 'sta_CloseSocketBusy'}
METHOD PROTECTED sta_CloseSocketBusy : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (stStateMachine.intState<>stStateMachine.intStateLastCycle) THEN
		stStateMachine.intStateHistory := stStateMachine.intStateLastCycle;
		stStateMachine.intStateLastCycle := stStateMachine.intState;
		
		// Reset pending commands			
		bolExecuteSend:= FALSE;
		bolExecuteRead:= FALSE;
		// Reset Rx/Tx states
		intRxState:= cRxIdle;
		intTxState:= cTxIdle;
		// Close socket
		fbClientConnection.bEnable:= FALSE;

	END_IF

//--------------------------------------------------------------------------------------
// Cyclic action
	IF (stStateMachine.intState=stStateMachine.intStateLastCycle) THEN
		
	
		// State Change Condition

		// Check about FUB error	
		IF (fbClientConnection.bError = TRUE) OR (fbClientConnection.nErrId <> 0) THEN
			SIf.stErr.bolErrorActive := TRUE;	
			SIf.stErr.bolSocketError := TRUE;
			SIf.stErr.udiErrorID := fbClientConnection.nErrId;
			stStateMachine.intStateNext := cBCD_STA_Error;
		END_IF
	
		// Wait until socket is closed	
		IF (fbClientConnection.eState = eSOCKET_CLOSED) THEN
			stStateMachine.intStateNext := cBCD_STA_WaitForJob;
		ELSIF  (SIf.stErr.bolErrorActive= TRUE) THEN 
			stStateMachine.intStateNext := cBCD_STA_Error;		
		END_IF
	
	END_IF

//--------------------------------------------------------------------------------------
// Exit action
	IF (stStateMachine.intState<>stStateMachine.intStateNext) THEN
		SIf.intCmd := cBCD_CMD_DONE;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="CycleEndDuty" Id="{6991b51e-f6c1-4114-833e-5ce5a16ba432}">
      <Declaration><![CDATA[{attribute 'object_name' := 'CycleEndDuty'}
METHOD PROTECTED CycleEndDuty : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SIf.strState := THIS^.getCmdState(intCmdState:=stStateMachine.intState,bolState:=TRUE);

IF (stStateMachine.intState<>stStateMachine.intStateNext) THEN
	stStateMachine.intState := stStateMachine.intStateNext;  // set next state for next cycle
END_IF

(***************************************************************
        Function Block Calls
***************************************************************)

// Cyclic call of method "HandleTxData"
THIS^.HandleTxData();]]></ST>
      </Implementation>
    </Method>
    <Method Name="HandleRxData" Id="{74c0ff1a-cafe-4771-9bc8-4f903dd97345}">
      <Declaration><![CDATA[{attribute 'object_name' := 'HandleRxData'}
METHOD PRIVATE HandleRxData
VAR
	uinI: UINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// GET/FETCH RX DATA
CASE intRxState OF
	
	// wait for read command
	cRxIdle:
		IF (bolExecuteRead=TRUE) AND (fbClientConnection.eState = eSOCKET_CONNECTED)THEN	

			// clear rx buffer
			FOR uinI:= 0 TO SIZEOF(aRxData)-1 DO
				aRxData[uinI]:= 16#00;
			END_FOR
			// Delete last message
			strReceivedMessage:= '';
			udiReceivedBytes := 0;
								
			// receive frame			
			fbReceive( bExecute := FALSE );
			fbReceive( 	sSrvNetId	:= '',
						hSocket		:= fbClientConnection.hSocket,
						cbLen		:= SIZEOF(aRxData),
						pDest		:= ADR(aRxData),
						bExecute	:= TRUE,
						tTimeout	:= SIf.stPar.tTOReceive);
			intRxState := cRxBusy;		
		END_IF
		
	// wait until receive not busy
	cRxBusy:
		fbReceive( bExecute := FALSE );
		IF NOT fbReceive.bBusy THEN
			IF NOT fbReceive.bError THEN
				IF (fbReceive.nRecBytes > 0) THEN (* some bytes received *)
					udiReceivedBytes := fbReceive.nRecBytes;
					intRxState := cRxConv; // convert received message
				ELSE
					// Try to read again
					intRxState := cRxIdle;
				END_IF	
			// Error	
			ELSE
				IF (SIf.stErr.bolErrorActive = FALSE) THEN
					SIf.stErr.bolErrorActive := TRUE;	
					SIf.stErr.udiErrorID := fbReceive.nErrId;	
					// Try to close the socket. Needs reconnect for socket after an error!	
					stStateMachine.intStateNext := cBCD_STA_CloseSocketBusy;	
				END_IF	
			END_IF			
		END_IF
			
	// convert received message to string
	cRxConv: 
		THIS^.PrepareReadMsg();
		THIS^.CheckSensorError(); // Error response: [STX]sFA <x>[ETX]    	
		bolExecuteRead:= FALSE; // Read done and data converted to string format
		intRxState := cRxIdle;
		
END_CASE
]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_Error" Id="{82b7a404-d88f-4473-ba9c-3069496ba777}">
      <Declaration><![CDATA[{attribute 'object_name' := 'sta_Error'}
METHOD PROTECTED sta_Error : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (stStateMachine.intState<>stStateMachine.intStateLastCycle) THEN
		stStateMachine.intStateHistory := stStateMachine.intStateLastCycle;
		stStateMachine.intStateLastCycle := stStateMachine.intState;
		
		// Reset pending commands			
		bolExecuteSend:= FALSE;
		bolExecuteRead:= FALSE;
		// Reset Rx/Tx states
		intRxState:= cRxIdle;
		intTxState:= cTxIdle;
		
	END_IF


//--------------------------------------------------------------------------------------
// Cyclic action
	IF (stStateMachine.intState=stStateMachine.intStateLastCycle) THEN
			
		// State Change Condition
		IF (SIf.stErr.bolErrAck = TRUE) THEN
			// Reset all active errors
			SIf.stErr.bolErrorActive := FALSE;
			SIf.stErr.bolSocketError := FALSE;
			SIf.stErr.bolSensorError := FALSE;
			SIf.stErr.udiErrorID := 0;
			SIf.stErr.bolErrAck := FALSE;
			// Needs reconnect for socket after an error!		
			stStateMachine.intStateNext := cBCD_STA_CloseSocketBusy;		
		END_IF
		
	END_IF

//--------------------------------------------------------------------------------------
// Exit action
	IF (stStateMachine.intState<>stStateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="StateMachine" Id="{85bf2f8e-a78a-43fa-bbb7-de6999e2d4bc}">
      <Declaration><![CDATA[{attribute 'object_name' := 'StateMachine'}
METHOD PROTECTED StateMachine : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// main statemachine
CASE stStateMachine.intState OF	
	cBCD_STA_WaitForJob:
   		THIS^.sta_WaitForJob();
	cBCD_STA_ConnectSocketBusy: 
        THIS^.sta_ConnectSocketBusy();	
	cBCD_STA_CloseSocketBusy: 
        THIS^.sta_CloseSocketBusy();		
	cBCD_STA_SocketClosed: 
        THIS^.sta_SocketClosed();	
	cBCD_STA_ComTestBusy:
	    THIS^.sta_ComTestBusy();
	cBCD_STA_ReadCodeBusy:
	    THIS^.sta_ReadCodeBusy();
    cBCD_STA_Error: 
        THIS^.sta_Error();
END_CASE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="getSIf" Id="{86fc9feb-45c7-4163-830f-c53ee419e66f}">
      <Declaration><![CDATA[{attribute 'object_name' := 'getSIf'}
METHOD getSIf : POINTER TO T_BCD
]]></Declaration>
      <Implementation>
        <ST><![CDATA[getSIf := ADR(SIf);]]></ST>
      </Implementation>
    </Method>
    <Action Name="ClearTxBuffer" Id="{8c46fc43-76b4-4f74-95d3-37737a30e53c}">
      <Implementation>
        <ST><![CDATA[Tc2_System.MEMSET(ADR(rTxData.aTxData),16#00,SIZEOF(rTxData.aTxData));
]]></ST>
      </Implementation>
    </Action>
    <Method Name="getCmdState" Id="{8fb68db1-6cc4-4222-a9c4-8c5478851770}">
      <Declaration><![CDATA[{attribute 'object_name' := 'getCmdState'}
METHOD PROTECTED getCmdState : STRING
VAR_INPUT
	intCmdState: INT;
	bolState: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// get string cmd/state string information in runtime
IF (bolState=FALSE) THEN
	
// cmd's
	CASE intCmdState OF
		cBCD_CMD_DONE: 
			getCmdState := 'cBCD_CMD_DONE';
		cBCD_CMD_CONNECT_SOCKET: 
			getCmdState := 'cBCD_CMD_CONNECT_SOCKET';
		cBCD_CMD_CLOSE_SOCKET: 
			getCmdState := 'cBCD_CMD_CLOSE_SOCKET';
		cBCD_CMD_READ_BARCODE: 
			getCmdState := 'cBCD_CMD_READ_BARCODE';		
		cBCD_CMD_COM_TEST: 
			getCmdState := 'cBCD_CMD_COM_TEST';			
	END_CASE;
ELSE

// state's
	CASE intCmdState OF
		cBCD_STA_WaitForJob: 
			getCmdState := 'cBCD_STA_WaitForJob';
		cBCD_STA_ConnectSocketBusy: 
			getCmdState := 'cBCD_STA_ConnectSocketBusy';
		cBCD_STA_CloseSocketBusy: 
			getCmdState := 'cBCD_STA_CloseSocketBusy';
		cBCD_STA_SocketClosed: 
			getCmdState := 'cBCD_STA_SocketClosed';	
		cBCD_STA_ReadCodeBusy: 
			getCmdState := 'cBCD_STA_ReadCodeBusy';	
		cBCD_STA_ComTestBusy:
			getCmdState := 'cBCD_STA_ComTestBusy';	
		cBCD_STA_Error: 
			getCmdState := 'cBCD_STA_Error';
	END_CASE;
	
END_IF

]]></ST>
      </Implementation>
    </Method>
    <Method Name="HandleTxData" Id="{98cf61cd-8d55-4d42-a889-0dcddf7e415e}">
      <Declaration><![CDATA[{attribute 'object_name' := 'HandleTxData'}
METHOD PRIVATE HandleTxData
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// HANDLE TX DATA
CASE intTxState OF
	
	// wait for send command (ready for send)
	cTxIdle:
		IF (bolExecuteSend) AND (fbClientConnection.eState = eSOCKET_CONNECTED)THEN									
			// send frame
			fbSend( bExecute := FALSE );
			fbSend(	sSrvNetID 	:= '',
					hSocket		:= fbClientConnection.hSocket,
					cbLen		:= rTxData.intMessageSize,
					pSrc		:= ADR(rTxData.aTxData),					// pointer to the frame data
					bExecute	:= TRUE,
					tTimeout 	:= SIf.stPar.tTOSend);
	
			intTxState := cTxBusy;							
		END_IF
		
	// wait until send not busy
	cTxBusy:
		fbSend( bExecute := FALSE );
		IF NOT (fbSend.bBusy) THEN
			intTxState := cTxIdle;
			// Check about FUB error	
			IF (fbSend.bError = TRUE) THEN
				IF (SIf.stErr.bolErrorActive = FALSE) THEN
					SIf.stErr.bolErrorActive := TRUE;	
					SIf.stErr.udiErrorID := fbSend.nErrId;	
					// Try to close the socket. Needs reconnect for socket after an error!	
					stStateMachine.intStateNext := cBCD_STA_CloseSocketBusy;	
				END_IF				
			// no error
			ELSE
				udiNSend := udiNSend + 1; // increment the number of send frames
				bolExecuteSend:= FALSE;
			END_IF
		END_IF
		
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_ComTestBusy" Id="{99a1b8ab-8b22-441c-8645-6eb1864c1e85}">
      <Declaration><![CDATA[{attribute 'object_name' := 'sta_ComTestBusy'}
METHOD PROTECTED sta_ComTestBusy : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (stStateMachine.intState<>stStateMachine.intStateLastCycle) THEN
		stStateMachine.intStateHistory := stStateMachine.intStateLastCycle;
		stStateMachine.intStateLastCycle := stStateMachine.intState;

		// Clear data
		SIf.stData.stDevInfo.strComTestResult:='';
		SIf.stStatus.bolComChecked:=FALSE;
	
		// Init sub state machine
		stSubStateMachine.intState := cBCD_SUB_Send;
		stSubStateMachine.intStateNext := cBCD_SUB_Send;	
	END_IF

//--------------------------------------------------------------------------------------
// Cyclic action
	IF (stStateMachine.intState=stStateMachine.intStateLastCycle) THEN
			
		// sub state machine for reading the bar code	
		CASE stSubStateMachine.intState OF

			cBCD_SUB_Send:
				// Check Tx state idle
				IF (intTxState = cTxIdle) THEN
					CASE SIf.stCfg.iSensorType OF
						E_BCD_Type.SICK:
							THIS^.PrepareSendMsg('sRI0'); // Call SOPAS command "sRI0"
							
						E_BCD_Type.KEYENCE:
							THIS^.PrepareSendMsg('BCLR'); // clear send buffer
					END_CASE
					bolExecuteSend:= TRUE;
					stSubStateMachine.intState := cBCD_SUB_Wait;
				END_IF
		
			cBCD_SUB_Wait:
				// Wait until executed -> flag set back to false by method handle TxData
				IF (bolExecuteSend=FALSE)THEN
					stSubStateMachine.intState := cBCD_SUB_Read;
				END_IF
				
			cBCD_SUB_Read:
				// Check Rx state idle
				IF (intRxState = cRxIdle) THEN
					bolExecuteRead:= TRUE;
					stSubStateMachine.intState := cBCD_SUB_CheckAnswer;
				END_IF
			
			cBCD_SUB_CheckAnswer:
				// Wait until executed -> flag set back to false by method handle RxData
				IF (bolExecuteRead=FALSE)THEN				
					// Check about corresponding answer for the command	
					CASE SIf.stCfg.iSensorType OF
						E_BCD_Type.SICK:					
							IF (TC2_STANDARD.FIND(strReceivedMessage,'sRA') <> 0) THEN
								SIf.stData.stDevInfo.strComTestResult:=strReceivedMessage;
								SIf.stStatus.bolComChecked:=TRUE;
							END_IF
						
						E_BCD_Type.KEYENCE:
							IF (TC2_STANDARD.FIND(strReceivedMessage,'OK,BCLR') <> 0) THEN
								SIf.stData.stDevInfo.strComTestResult:=strReceivedMessage;
								SIf.stStatus.bolComChecked:=TRUE;
							END_IF
					END_CASE
					stSubStateMachine.intState := cBCD_SUB_Done;
				END_IF
			
			cBCD_SUB_Done:
				; // job executed
			
		END_CASE
		
		 // job executed
		IF (stSubStateMachine.intState = cBCD_SUB_Done) THEN
			stStateMachine.intStateNext := cBCD_STA_WaitForJob;	
		END_IF
	
	END_IF

//--------------------------------------------------------------------------------------
// Exit action
	IF (stStateMachine.intState<>stStateMachine.intStateNext) THEN
		SIf.intCmd := cBCD_CMD_DONE;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{b6d626b0-4836-4bae-8d83-6e5cf1aff188}">
      <Declaration><![CDATA[{attribute 'object_name' := 'FB_init'}
METHOD FB_init : BOOL
VAR_INPUT
	///if TRUE, the retain variables are initialized (warm start / cold start)
	bInitRetains: BOOL;
	///if TRUE, the instance afterwards gets moved into the copy code (online change)
	bInCopyCode: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Communication Settings
SIf.stCfg.strSensorIPAdr := '192.168.1.103';  //'192.168.0.10'; 
SIf.stCfg.udiSensorPort := 9004;
SIf.stCfg.bolResponseIncludingDiagnostics := FALSE;
SIf.stCfg.iSensorType := E_BCD_Type.KEYENCE;

// Parameter
SIf.stPar.tTOConnection := T#50S;
SIf.stPar.tTOReceive := T#5S;
SIf.stPar.tTOSend := T#5S;]]></ST>
      </Implementation>
    </Method>
    <Method Name="PrepareBarCodeData" Id="{d5e1ae75-4f39-477a-a1bb-21f1080191ae}">
      <Declaration><![CDATA[{attribute 'object_name' := 'PrepareBarCodeData'}
METHOD PRIVATE PrepareBarCodeData
VAR
	intI: INT;
	intStartIndex: INT;
	intStartIndexCode: INT;
	bASCIISign: BYTE;
	strCharacter: STRING(1);
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// Note: Currently only the bar code is read out from the received message. "OI=" section in message.
// Other data sections like e.g. "TT=", "OTL=", "CC=" are not prepared.

// ----------------------------------------------------
// Get bar code and write result to structure "stData"
// ----------------------------------------------------
	
// Example of message: <stx> <CR><LF> TT=125ms OTL=0mm CC=1 OI=25<CR><LF>45376049874<CR><LF> .... and so on ...   <etx>
				 
// Get start index for bar code 'OI='
intStartIndex := TC2_STANDARD.FIND(strReceivedMessage,'OI=');

// Loop over received message to get the bar code
FOR intI:=intStartIndex TO LEN(strReceivedMessage)-intStartIndex DO	
	// Convert to ASCII to detect CR and LF
	bASCIISign:= F_ToASC(Mid(strReceivedMessage,1,intI));
	// The bar code number starts after LF					
	IF (bASCIISign = cLF) THEN
		// Save start index of the bar code
		intStartIndexCode:= intI + 1;
	END_IF
	// Read every single number of the bar code until the end "CR"
	IF ((intStartIndexCode <> 0) AND (intI >= intStartIndexCode)) THEN
		// As long as the end is not reached...(The bar code number ends with CR)	
		IF (bASCIISign <> cCR) THEN								
			// Write result to structure stData	
			strCharacter:= Mid(strReceivedMessage,1,intI);
			SIf.stData.strBarCode:= CONCAT(SIf.stData.strBarCode,strCharacter);	
			SIf.stData.uliBarCode:= STRING_TO_ULINT(SIf.stData.strBarCode);
			SIf.stData.bolCodeReaded:= TRUE;																	
		ELSE
			// end of bar code reached "CR" -> EXIT loop
			EXIT;
		END_IF
	END_IF
	
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="CheckSensorError" Id="{e2fa62b7-d503-4d82-902e-027602ebecdc}">
      <Declaration><![CDATA[{attribute 'object_name' := 'CheckSensorError'}
METHOD PRIVATE CheckSensorError
VAR
	strErrorID: STRING(3);
	bytErrorID: BYTE;
	cbData: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[(*--------------------------------------------------------------------------
IF an error occurs the Sopas command is answered with:
[STX]sFA <x>[ETX] as an error response

	Error list: <x> can have following values:
	1:	Access denied
	2:	Unknown Index
	3:	Unknown Index
	4:	Wrong Condition
	5:	Invalid Data
	6:	Unknown Error
	7:	Too Many Parameter
	8:	Parameter Missing
	9: 	Wrong Parameter
	A:	No Write Access
	B:	Unknown Command
	C:	Unknown Command
	D:	Server Busy
	E:	Textstring Too Long
	F:	Unknown Event
	10:	Too many Parameter
	11: Invalid Character
	12.	No Message
	13:	No Answer
	14:	Internal Error
	15:	HubAddress: wrong
	16:	HubAddress: error
	17:	HubAddress: error

---------------------------------------------------------------------------*)

// If received message is an error response from sensor: [STX]sFA <x>[ETX]    				
IF (TC2_STANDARD.FIND(strReceivedMessage,'sFA') <> 0) THEN
	
	// Get error number
	strErrorID := Mid(strReceivedMessage,LEN(strReceivedMessage)-1,5);
	cbData := HEXSTR_TO_DATA( strErrorID, ADR(bytErrorID), SIZEOF(bytErrorID));

	// No interpretation of the error number, only set SensorError with error number info 
	(*
	CASE bytErrorID OF
		16#1: 
		16#2:
		16#3:	
		16#A:			
		16#B,16#C:
	END_CASE
	*)
	
	IF (SIf.stErr.bolErrorActive = FALSE) THEN
		SIf.stErr.bolErrorActive:= TRUE;	
		SIf.stErr.bolSensorError:= TRUE; 
		SIf.stErr.udiErrorID := bytErrorID;	
		// Try to close the socket. Needs reconnect for socket after an error!	
		stStateMachine.intStateNext := cBCD_STA_CloseSocketBusy;	
	END_IF	

END_IF
	]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_ReadCodeBusy" Id="{e54c24e3-afec-45d9-9a45-a1bd55bc6eb2}">
      <Declaration><![CDATA[{attribute 'object_name' := 'sta_ReadCodeBusy'}
METHOD PROTECTED sta_ReadCodeBusy : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (stStateMachine.intState<>stStateMachine.intStateLastCycle) THEN
		stStateMachine.intStateHistory := stStateMachine.intStateLastCycle;
		stStateMachine.intStateLastCycle := stStateMachine.intState;
		
		// Clear data
		SIf.stData.uliBarCode:=0;
		SIf.stData.strBarCode:='';
		SIf.stData.bolCodeReaded:= FALSE;
						
		// Init sub state machine
		stSubStateMachine.intState := cBCD_SUB_Send;
		stSubStateMachine.intStateNext := cBCD_SUB_Send;	
	END_IF

//--------------------------------------------------------------------------------------
// Cyclic action
	IF (stStateMachine.intState=stStateMachine.intStateLastCycle) THEN
			
		// sub state machine for reading the bar code	
		CASE stSubStateMachine.intState OF

			cBCD_SUB_Send:
				// Check Tx state idle
				IF (intTxState = cTxIdle) THEN
					CASE SIf.stCfg.iSensorType OF
						E_BCD_Type.SICK:
							THIS^.PrepareSendMsg('sMN mTCgateon'); // Call SOPAS method "sMN mTCgateon"
							bolExecuteSend:= TRUE;
							stSubStateMachine.intState := cBCD_SUB_Wait;
							
						E_BCD_Type.KEYENCE:
							THIS^.PrepareSendMsg('LON'); // Call Trigger ON
							bolExecuteSend:= TRUE;
							stSubStateMachine.intState := cBCD_SUB_Wait;
					ELSE
						SIf.stData.strBarCode:= 'no sensor type defined';
						SIf.stData.uliBarCode:= 0;
					END_CASE
					
				END_IF
		
			cBCD_SUB_Wait:
				// Wait until executed -> flag set back to false by method handle TxData
				IF (bolExecuteSend=FALSE)THEN
					stSubStateMachine.intState := cBCD_SUB_Read;
				END_IF
				
			cBCD_SUB_Read:
				// Check Rx state idle
				IF (intRxState = cRxIdle) THEN
					bolExecuteRead:= TRUE;
					stSubStateMachine.intState := cBCD_SUB_CheckAnswer;
				END_IF
			
			cBCD_SUB_CheckAnswer:
				// Wait until executed -> flag set back to false by method handle RxData
				IF (bolExecuteRead=FALSE)THEN				
					// Check about corresponding answer for the called method
					CASE SIf.stCfg.iSensorType OF
						E_BCD_Type.SICK:
							IF (FC_StrComp(strReceivedMessage,'sAN mTCgateon 1') = 0) THEN			
								// Check Rx state idle
								IF (intRxState = cRxIdle) THEN
									bolExecuteRead:= TRUE;
									stSubStateMachine.intState := cBCD_SUB_GetData;
								END_IF
							END_IF
							
						E_BCD_Type.KEYENCE:
							IF (FC_StrComp(strReceivedMessage,'OK,LON') = 0) THEN			
								// Check Rx state idle
								IF (intRxState = cRxIdle) THEN
									bolExecuteRead:= TRUE;
									stSubStateMachine.intState := cBCD_SUB_GetData;
								END_IF
							END_IF
					END_CASE
				END_IF
			
			cBCD_SUB_GetData:
				// Wait until executed -> flag set back to false by method handle RxData
				IF (bolExecuteRead=FALSE)THEN
					
					CASE SIf.stCfg.iSensorType OF
						E_BCD_Type.SICK:
							// No barcode recognized after the end of the time window ("duration" setting of sensor object trigger), NoRead !
							IF (((TC2_STANDARD.FIND(strReceivedMessage,'OI=') <> 0) OR NOT SIf.stCfg.bolResponseIncludingDiagnostics) 
								AND (TC2_STANDARD.FIND(strReceivedMessage,'*NoRead*') <> 0)) THEN			
								SIf.stData.strBarCode:='NoRead';
								stSubStateMachine.intState := cBCD_SUB_Done;
							END_IF
							
							// Barcode detected within the time window
							IF (((TC2_STANDARD.FIND(strReceivedMessage,'OI=') <> 0) OR NOT SIf.stCfg.bolResponseIncludingDiagnostics) 
								AND NOT (TC2_STANDARD.FIND(strReceivedMessage,'*NoRead*') <> 0)) THEN			
								// Prepare bar code data from received message
								THIS^.PrepareBarCodeData();
								stSubStateMachine.intState := cBCD_SUB_Done;
							END_IF
						
						E_BCD_Type.KEYENCE:
							// No barcode recognized after the end of the time window ("duration" setting of sensor object trigger), NoRead !
							IF (TC2_STANDARD.FIND(strReceivedMessage,'ERROR') <> 0) THEN			
								SIf.stData.strBarCode:='NoRead';
								stSubStateMachine.intState := cBCD_SUB_Done;
							END_IF
							
							// Barcode detected within the time window
							IF NOT (TC2_STANDARD.FIND(strReceivedMessage,'ERROR') <> 0) THEN			
								// Prepare bar code data from received message
								iStringLength := Tc2_Standard.LEN(strReceivedMessage);
								SIf.stData.strBarCode := Tc2_Standard.LEFT(strReceivedMessage,iStringLength-1);
								SIf.stData.uliBarCode:= STRING_TO_ULINT(SIf.stData.strBarCode);
								stSubStateMachine.intState := cBCD_SUB_Done;
							END_IF
					END_CASE
				END_IF
			
			cBCD_SUB_Done:
				; // job executed
			
		END_CASE
		
		 // job executed
		IF (stSubStateMachine.intState = cBCD_SUB_Done) THEN
			stStateMachine.intStateNext := cBCD_STA_WaitForJob;	
		END_IF
			
	END_IF

//--------------------------------------------------------------------------------------
// Exit action
	IF (stStateMachine.intState<>stStateMachine.intStateNext) THEN
		SIf.intCmd := cBCD_CMD_DONE;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="PrepareSendMsg" Id="{e67cfade-0958-4a28-abdc-00b43b64367e}">
      <Declaration><![CDATA[{attribute 'object_name' := 'PrepareSendMsg'}
METHOD PRIVATE PrepareSendMsg
VAR_INPUT
	strMsg: STRING;
END_VAR
VAR
	intI: UINT;
	strCharacter: STRING(1);
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Clear send buffer
ClearTxBuffer();

// STX - start of telegramm
rTxData.aTxData[0]:= cSTX;

// Loop over message string and convert to ASCII
FOR intI:= 1 TO INT_TO_UINT(LEN(strMsg)) DO
	// Get char
	strCharacter:= MID(strMsg,1,UINT_TO_INT(intI));
	// Convert to ASCII
	rTxData.aTxData[intI]:= F_ToASC(strCharacter);
END_FOR

// ETX - end of telegramm
rTxData.aTxData[intI]:= cETX;

rTxData.intMessageSize := intI + 1;]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_WaitForJob" Id="{e79427c7-26eb-403f-9f44-c99aabec868f}">
      <Declaration><![CDATA[{attribute 'object_name' := 'sta_WaitForJob'}
METHOD PROTECTED sta_WaitForJob : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (stStateMachine.intState<>stStateMachine.intStateLastCycle) THEN
		stStateMachine.intStateLastCycle := stStateMachine.intState;
	END_IF


//--------------------------------------------------------------------------------------
// Cyclic action
	IF (stStateMachine.intState=stStateMachine.intStateLastCycle) THEN

		// State Change Condition
		IF (SIf.intCmd=cBCD_CMD_CONNECT_SOCKET) THEN						// Connect TCP socket
			stStateMachine.intStateNext := cBCD_STA_ConnectSocketBusy;
		ELSIF (SIf.intCmd=cBCD_CMD_CLOSE_SOCKET) THEN						// Close TCP socket
			stStateMachine.intStateNext := cBCD_STA_CloseSocketBusy;
		ELSIF (SIf.intCmd=cBCD_CMD_COM_TEST) THEN							// Communication test 
			stStateMachine.intStateNext := cBCD_STA_ComTestBusy;	
		ELSIF (SIf.intCmd=cBCD_CMD_READ_BARCODE) THEN						// Read bar code
			stStateMachine.intStateNext := cBCD_STA_ReadCodeBusy;				
		END_IF

				
	END_IF

//--------------------------------------------------------------------------------------
// Exit action
	IF (stStateMachine.intState<>stStateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="CtrlInit" Id="{fab64fc1-06ea-49cf-b0b0-ae06254ffffc}">
      <Declaration><![CDATA[{attribute 'object_name' := 'CtrlInit'}
METHOD PROTECTED CtrlInit : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// one time first cyclic call
// The socket is closed
stStateMachine.intStateNext := cBCD_STA_SocketClosed;]]></ST>
      </Implementation>
    </Method>
    <ObjectProperties />
  </POU>
</TcPlcObject>